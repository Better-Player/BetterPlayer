import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '6.1.0'
}

mainClassName = 'net.betterplayer.betterplayer.BetterPlayer'

configurations {
	proprietary
	proprietary.extendsFrom compile
}

allprojects {

	group = 'net.betterplayer.betterplayer'
	version = '1.0'
	
	sourceCompatibility = 1.11
	targetCompatibility = 1.11

	repositories {
		maven { url 'https://jitpack.io' }
		jcenter()
		mavenCentral()
		mavenLocal()
		
		flatDir {
			dirs 'libs'
		}
    }
}

dependencies {
	//JDA, discord API library
	compile 'net.dv8tion:JDA:4.2.0_227'
	
	//SLF4J, logging
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.29'
	
	//LavaPlayer, Discord sound library
	compile 'com.sedmelluq:lavaplayer:1.3.66'

	//SnakeYAML, for the config.yml file
	compile 'org.yaml:snakeyaml:1.27'
	
	//JSON, for communicating with external APIs
	compile 'org.json:json:20201115'

	//Apache Commons
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

	//HttpLib, for communicating with external APIs
	compile 'com.github.TheDutchMC:HttpLib:1.1'
	
	//MySQL connector
	compile ("mysql:mysql-connector-java:8.0.21") {
    	exclude module: 'protobuf-java'
    }
	
	//Common classes to use with proprietary
    compile 'net.betterplayer:BetterPlayerCommon:1.0.2'
	
	//Proprietary code
	//proprietary 'net.betterplayer:auth:1.0.9'
	proprietary 'net.betterplayer:libbetterplayer:1.0.40'
}

//Without this Eclipse wont pick up on the proprietary dependencies
/*eclipse {
	classpath {
		plusConfigurations += [configurations.proprietary]
	}
}*/

task proprietaryJar(type: ShadowJar) {
   	mainClassName = 'net.betterplayer.betterplayer.BetterPlayer'

	description 'Build a proprietary jar'
	
	archiveClassifier = 'PROPRIETARY'
 	
 	manifest {
    	attributes('Main-Class': mainClassName)
  	}
	
	from sourceSets.main.output
	configurations = [project.configurations.runtime, project.configurations.proprietary]
	
	destinationDirectory = file("$rootDir/releases")	
}

task releaseJar(type: ShadowJar) {
	description 'Build a release Jar'
	
	archiveClassifier = 'RELEASE'
  	manifest {
    	attributes('Main-Class': mainClassName)
  	}
	
	from sourceSets.main.output
	configurations = [project.configurations.runtime]
	
	destinationDirectory = file("$rootDir/releases")
}

//This task is for GitHub Actions only
task ghActions(type: ShadowJar) {	
    manifest.inheritFrom project.tasks.jar.manifest
	
	description 'GitHub Actions Task. Do not use in regular development!'
	
	version = ''
	archiveClassifier = ''
	archiveBaseName = 'output'

 	manifest {
    	attributes('Main-Class': mainClassName)
  	}

	from sourceSets.main.output
	configurations = [project.configurations.runtime]
	
	destinationDirectory = file("$rootDir/actions")
}